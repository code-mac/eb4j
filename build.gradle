group 'com.apehat'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

configure(allprojects) { project ->

    group 'com.apehat'
    version '1.0-SNAPSHOT'

    apply plugin: 'java'

    dependencies {
        testCompile group: 'org.testng', name: 'testng', version: '6.14.3'
    }

    ext.moduleName = 'com.apehat.' + project.name.replace('-', '.')

    compileJava {
        sourceCompatibility = 10
        targetCompatibility = 10
        options.encoding = 'UTF-8'

        inputs.property("moduleName", moduleName)
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath,
            ]
            classpath = files()
        }
    }

    compileTestJava {
        sourceCompatibility = 10
        targetCompatibility = 10
        options.encoding = 'UTF-8'
        inputs.property("moduleName", moduleName)
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath,
                    '--add-modules', 'org.testng',
                    '--add-reads', "$moduleName=org.testng",
                    '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
            ]
            classpath = files()
        }
    }

    test {
        inputs.property("moduleName", moduleName)
        doFirst {
            jvmArgs = [
                    '--module-path', classpath.asPath,
                    '--add-modules', 'ALL-MODULE-PATH',
                    '--add-reads', "$moduleName=org.testng",
                    '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
            ]
            classpath = files()
        }
    }
}